set(CMAKE_SYSTEM_NAME Linux)
set(CMAKE_SYSTEM_PROCESSOR mips)

find_package(Python REQUIRED COMPONENTS Interpreter)
set(PYTHON_EXECUTABLE
    "${Python_EXECUTABLE}"
    CACHE FILEPATH "Python3 interpreter")

set(MIPS_BINUTILS_PREFIX mips-linux-gnu-)
set(TOOLS_DIR ${CMAKE_SOURCE_DIR}/tools)

# Install IDO compilers
execute_process(COMMAND ${CMAKE_COMMAND} -P ${CMAKE_SOURCE_DIR}/cmake/IDO.cmake WORKING_DIRECTORY ${CMAKE_SOURCE_DIR})

set(CMAKE_C_COMPILER_LAUNCHER ${PYTHON_EXECUTABLE} ${TOOLS_DIR}/asm-processor/build.py)
find_program(TOOLCHAIN_C_COMPILER_EXE cc HINTS "${TOOLS_DIR}/ido/linux/7.1/" REQUIRED)
set(CMAKE_C_COMPILER
    ${TOOLCHAIN_C_COMPILER_EXE}
    --
    ${CMAKE_ASM_COMPILER}
    -I${CMAKE_SOURCE_DIR}/include
    -march=vr4300
    -EB
    --
    CACHE STRING "Ido C compiler")
set(CMAKE_C_COMPILER_WORKS TRUE)

set(TOOLCHAIN_ASM_COMPILER_EXE ${MIPS_BINUTILS_PREFIX}as)
find_program(CMAKE_ASM_COMPILER ${TOOLCHAIN_ASM_COMPILER_EXE} HINTS "/usr/bin" REQUIRED)
set(CMAKE_ASM_COMPILER
    "${CMAKE_ASM_COMPILER}"
    CACHE FILEPATH "GNU Mips assembler")
set(CMAKE_ASM_COMPILE_OBJECT "<CMAKE_ASM_COMPILER> <DEFINES> <INCLUDES> <FLAGS> -o <OBJECT> <SOURCE>")

set(TOOLCHAIN_LINKER_EXE ${MIPS_BINUTILS_PREFIX}ld)
find_program(CMAKE_LINKER ${TOOLCHAIN_LINKER_EXE} HINTS "/usr/bin" REQUIRED)
set(CMAKE_LINKER
    "${CMAKE_LINKER}"
    CACHE FILEPATH "GNU Mips linker")
set(CMAKE_C_LINK_EXECUTABLE "<CMAKE_LINKER> <LINK_FLAGS> <OBJECTS> -o <TARGET>")

set(TOOLCHAIN_OBJCOPY_EXE ${MIPS_BINUTILS_PREFIX}objcopy)
find_program(CMAKE_OBJCOPY ${TOOLCHAIN_OBJCOPY_EXE} HINTS "/usr/bin" REQUIRED)
set(CMAKE_OBJCOPY
    "${CMAKE_OBJCOPY}"
    CACHE FILEPATH "GNU Mips objcopy")

set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE ONLY)
